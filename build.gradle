buildscript {
	repositories {
		mavenCentral()
		jcenter()
		maven {
			name = "Fabric"
			url = "http://maven.modmuss50.me/"
		}
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {
		classpath "net.fabricmc:fabric-loom:0.2.0-SNAPSHOT" //Don't backdate before 0.2.0; features in this version prevent messy workarounds!
		classpath 'org.ajoberstar:gradle-git:0.10.1'
	}
}

// see https://github.com/passsy/gradle-GitVersioner
ext.gitVersioner = [
        defaultBranch           : "frex",  // default "master"
        stableBranches          : ["frex"], // default [], the feature branch postfix (-dm4(6)) will not be appended on stable branches, all commits are included into the version number calculation
        yearFactor              : 1200, 	  // default "1000", increasing every 8.57h
        snapshotEnabled         : true,      // default false, the "-SNAPSHOT" postfix
        localChangesCountEnabled: false,       // default false, the (<commitCount>) before -SNAPSHOT
        shortName: { gitVersion ->            // optional closure to build a short name
          // allows you to add your own short name logic
          // All properties from gitVersion are available
          // can be used for CI `System.getenv("BUILD_NUMBER")`

          // i.e. use short sha1
          return gitVersion.commit.subSequence(0, 7)
        }
]

// import the script which runs the version generation
apply from: 'https://raw.githubusercontent.com/passsy/gradle-GitVersioner/master/git-versioner.gradle'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: "maven-publish"
apply plugin: net.fabricmc.loom.LoomGradlePlugin

repositories {
    flatDir {
        dirs '../renderbender/build/libs'
    }
 	maven {
    	name = "grondag"
    	url = "https://grondag-repo.appspot.com"
    	credentials {
            username "guest"
            password ""
		}
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

archivesBaseName = "canvas"
group = "grondag"
version = "${mod_version}.${gitVersionName}-${release_type}"

minecraft {
}

sourceSets {
	main {
	        java {
	            include 'grondag/**/*'
	            exclude '*.DS_Store'
	        }
	        resources
	        {
	        	include '**/*'
	        	exclude '*.DS_Store'
	        }
	    }
    test {
        java {
            exclude 'grondag/**'
        }
    }
}

dependencies {
	minecraft "com.mojang:minecraft:19w11b"
	mappings "net.fabricmc:yarn:19w11b.12"
	modCompile "net.fabricmc:fabric-loader:0.3.7.109"
	modCompile "net.fabricmc:fabric:0.2.3.111"
	modCompile "grondag:frex:0.1.72-alpha"
	modCompile "grondag:fermion:0.1.254-alpha"
	
	// should be removed before release
	modCompile "grondag:renderbender:0.1.73-alpha"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "https://grondag-repo.appspot.com") {
                authentication(userName: "admin", password: "${maven_password}")
            }
            pom.version = project.version
            pom.artifactId = project.archivesBaseName
            pom.groupId = project.group
        }
    }
}