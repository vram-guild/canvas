import net.fabricmc.loom.task.RemapJar

plugins {
	id 'fabric-loom' version '0.2.2-SNAPSHOT'
	id 'java'
	id 'maven'
	id 'maven-publish'
	id 'com.github.johnrengelman.shadow' version '4.0.3'
	id "com.zoltu.git-versioning" version "3.0.3"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

def versionInfo = ZoltuGitVersioning.versionInfo
def autoVersion = "${versionInfo.major}.${versionInfo.minor}.${versionInfo.commitCount}"

archivesBaseName = "canvas"
group = "grondag"
version = autoVersion;

minecraft {
}

repositories {
    maven { url "https://tehnut.info/maven"}
    maven { url "http://maven.sargunv.s3-website-us-west-2.amazonaws.com/" }
    maven { url "https://minecraft.curseforge.com/api/maven" }
    maven { url "https://maven.fabricmc.net/io/github/prospector/modmenu/ModMenu/" }
    maven { url "https://maven.jamieswhiteshirt.com/libs-release/"}
    
 	maven {
    	name = "grondag"
    	url = "https://grondag-repo.appspot.com"
    	credentials {
            username "guest"
            password ""
		}
    }
}

processResources {
	inputs.property "version", autoVersion
	from(sourceSets.main.resources.srcDirs) {
		include "fabric.mod.json"
		expand "version": version
	}
}

configurations {
    included {
        transitive = false
    }
}

shadowJar {
    version = autoVersion
    classifier = null
    configurations = [project.configurations.included]
}

task remapShadowJar(type: RemapJar) {
    jar = shadowJar.archivePath
}

tasks.remapShadowJar.dependsOn tasks.shadowJar

tasks.assemble.dependsOn tasks.remapShadowJar

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

jar {
	classifier = "slim"
	from "LICENSE"
}

sourceSets {
	main {
	        java {
	            include 'grondag/**/*'
	            exclude '*.DS_Store'
	        }
	        resources
	        {
	        	include '**/*'
	        	exclude '*.DS_Store'
	        }
	    }
    test {
        java {
            exclude 'grondag/**'
        }
    }
}

def mcVersion = "1.14.2 Pre-Release 2"

dependencies {
	minecraft "com.mojang:minecraft:$mcVersion"
	mappings "net.fabricmc:yarn:$mcVersion+build.4"
	modCompile "net.fabricmc:fabric-loader:0.4.7+build.147"
	modCompile "net.fabricmc.fabric-api:fabric-api:0.3.0-pre+build.164"
	
	modCompile "grondag:renderbender:0.5.2"
	
	modCompile "grondag:fermion:0.4.+"
	include "grondag:fermion:0.4.+"

	modCompile "grondag:frex:0.6.5"
	include "grondag:frex:0.6.5"
	
    modCompile "cloth-config:ClothConfig:0.2.1.14"
    include "cloth-config:ClothConfig:0.2.1.14"
	
	compile "blue.endless:jankson:1.1.0"
	included "blue.endless:jankson:1.1.0"
	
	// For dev env testing 
	modCompile "com.jamieswhiteshirt:developer-mode:1.0.11"
	modCompile "io.github.prospector.modmenu:ModMenu:1.5.4-85"
	//modCompile "cloth:ClothEvents:0.3.1.23" // for REI
	//modCompile "roughly-enough-items:RoughlyEnoughItems:2.7.11.97"
	
	// dev env annotation support
	compile "org.apiguardian:apiguardian-api:1.0.0"
	compile "com.google.code.findbugs:jsr305:3.0.2"
	
	// shadowed by FREX but needed in dev
	//compile "org.joml:joml:1.9.14"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
			artifact(jar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
			artifact(shadowJar) {
				builtBy remapShadowJar
			}
        }
    }

    repositories {
        maven {
            url "https://grondag-repo.appspot.com"
            credentials {
            	if (project.hasProperty('maven_user')){
					username = project.getProperty('maven_user')
				}
				if (project.hasProperty('maven_password')){
					password = project.getProperty('maven_password')
				}
			}
        }
    }
}