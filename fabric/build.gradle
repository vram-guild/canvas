// VRAM STANDARD GRADLE BUILD FOR FABRIC 1.20
// DO NOT MAKE CHANGES HERE - THEY WILL BE OVERWRITTEN BY AUTOMATED UPDATE

buildscript {
	dependencies {
		classpath 'org.kohsuke:github-api:1.301'
	}
}

plugins {
	id 'fabric-loom' version '1.5-SNAPSHOT'
	id 'maven-publish'
	id 'org.cadixdev.licenser' version '0.6.1'
	id 'org.ajoberstar.grgit' version '4.1.1'
	id 'com.matthewprenger.cursegradle' version '1.4.0'
	id 'checkstyle'
	id 'com.modrinth.minotaur' version '2.2.0'
}

ext.platform_name = 'fabric'
ext.loader_version = '0.15.6'
ext.fabric_version = '0.95.0+1.20.4'

// Hat tip to JellySquid
configurations {
    modIncludeImplementation
    modImplementation.extendsFrom modIncludeImplementation
    include.extendsFrom modIncludeImplementation
}

apply from: '../project_common.gradle'
apply from: 'project.gradle'
apply from: '../gruntle_common.gradle'

dependencies {
	minecraft "com.mojang:minecraft:${minecraft_version}"
	mappings loom.officialMojangMappings()
}

loom {
	mixin.defaultRefmapName = project.mod_name + "-refmap.json"

	if (project.hasProperty('accesswidener')) {
		accessWidenerPath = file(project.getProperty('accesswidener'))
	}
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

if (project.build_fat_jar) {
	task remapMavenJar(type: net.fabricmc.loom.task.RemapJarTask, dependsOn: jar) {
		input = jar.archiveFile
		archiveFileName = "${base.archivesName.get()}-${project.version}-slim.jar"
		addNestedDependencies = false
	}

	build.dependsOn remapMavenJar
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifactId = base.archivesName.get();
			from components.java
		}
	}

	repositories {
		mavenLocal()

		if (project.version.endsWith('LOCAL') || project.version.endsWith('SNAPSHOT')) {
			println "Snapshot build will be local only - not pushed to public maven"
		} else {
	    maven {
        url = 'sftp://maven.vram.io:22'
        credentials {
        	if (project.hasProperty('maven_user')) {
						username = project.getProperty('maven_user')
					}
					if (project.hasProperty('maven_password')) {
						password = project.getProperty('maven_password')
					}
				}
	    }
		}
	}
}

// Remainder unfortunately does not work in common script due to build script class path
// limitations and so must be duplicated for each platform script.
// Probably a way around it but Gradle has already exhausted my will to keep going.

import org.kohsuke.github.GHReleaseBuilder
import org.kohsuke.github.GitHub

task github(dependsOn: build) {
	onlyIf {
		project.github_releases_token
	}

	doLast {
		if (project.version.endsWith('LOCAL') || project.version.endsWith('SNAPSHOT')) {
			println "Snapshot build will be local only - not pushed to github"
		} else {
			def github = GitHub.connectUsingOAuth(project.github_releases_token)
			def repository = github.getRepository("${project.github_repository_owner}/${project.github_repository}")

			def releaseBuilder = new GHReleaseBuilder(repository, project.platform_name + '-' + project.version)
			releaseBuilder.name(project.platform_name + '-' + project.version)
			releaseBuilder.commitish(grgit.branch.current().name)

			def ghRelease = releaseBuilder.create()
			ghRelease.uploadAsset(file("${project.buildDir}/libs/${base.archivesName.get()}-${version}.jar"), "application/java-archive");
			ghRelease.uploadAsset(file("${project.buildDir}/libs/${base.archivesName.get()}-${version}-sources.jar"), "application/java-archive");
		}
	}
}

import com.modrinth.minotaur.TaskModrinthUpload

modrinth {
	if (project.hasProperty("modrinth_token") && project.hasProperty("modrinth_id")) {
		token = project.modrinth_token
		projectId = project.modrinth_id
		versionName = project.platform_name + '-' + project.version
		versionNumber = project.platform_name + '-' + project.version // Will fail if Modrinth has this version already
		versionType = project.release_type
		// On fabric, use 'remapJar' instead of 'jar'
		uploadFile = remapJar // This is the java jar task. If it can't find the jar, try 'jar.outputs.getFiles().asPath' in place of 'jar'
		gameVersions = ["${project.minecraft_version}"]
		loaders = ["fabric", "quilt"] // Must also be an array - no need to specify this if you're using Loom
	}
}
